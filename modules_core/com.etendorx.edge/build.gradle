plugins {
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id 'java'
    id 'maven-publish'
}

group = 'com.etendorx'
version = "$gradle.ext.version"
sourceCompatibility = JavaVersion.VERSION_17

repositories {
    mavenCentral()
}

publishing {
    publications {
        bootJava(MavenPublication) {
            groupId = group
            artifactId = 'edge'
            version = version
            artifact tasks.named("bootJar")
        }
    }
    repositories {
        maven {
            url "${repositoryUrl}"
            credentials {
                username = "${repositoryUser}"
                password = "${repositoryPassword}"
            }
        }
    }
}

ext {
    set('springCloudVersion', "2022.0.4")
}

dependencies {
    implementation 'org.springframework.cloud:spring-cloud-starter-config'
    implementation 'org.springframework.cloud:spring-cloud-starter-circuitbreaker-resilience4j'
    implementation 'org.springframework.cloud:spring-cloud-starter-gateway'
    implementation "org.springframework.boot:spring-boot-starter-actuator"

    implementation(project(':com.etendorx.utils.auth')) {
        exclude group: 'org.slf4j', module: '*'
    }
    annotationProcessor 'org.projectlombok:lombok:1.18.22'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-stub-runner'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

bootRun {
    debugOptions {
        port = Integer.valueOf((findProperty('debugPort') ?: port.get()) as String)
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

bootBuildImage {
    imageName = "${edgePushImage}"
    publish = true
    docker {
        publishRegistry {
            url = "${pushUrl}"
            username = "${pushUsername}"
            password = "${pushPassword}"
        }
    }
}

springBoot {
    buildInfo()
}

test.onlyIf { false }
