pluginManagement {
    repositories {
        mavenCentral()
        gradlePluginPortal()
        maven {
            url 'https://maven.pkg.github.com/etendosoftware/com.etendorx.gradlepluginrx'
            credentials {
                username "${githubUser}"
                password "${githubToken}"
            }
        }
    }
}

rootProject.name = 'etendorx'
var directories = ["libs", "modules_core", "modules_gen", "modules_test", "modules"]
gradle.ext.version = '2.3.4'

directories.each {
    def subDirs = []
    def queue = new LinkedList()
    queue.add(new File(it))

    while (!queue.isEmpty()) {
        def dir = queue.remove()
        if (new File(dir, 'build.gradle').isFile()) {
            subDirs.add(dir)
        } else {
            dir.listFiles(new FileFilter() {
                boolean accept(File file) {
                    return file.isDirectory()
                }
            })?.each {
                queue.add(it)
            }
        }
    }

    subDirs.each { File dir ->
        include dir.name
        project(":" + dir.name).projectDir = dir
    }
}
